#version 430

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D accumulation_image;
layout(rgba8, binding = 1) uniform image2D target_image;

// Tone mapping curve from Krzysztof Narkowicz
// https://knarkowicz.wordpress.com/2016/01/06/aces-filmic-tone-mapping-curve/
vec3 tone_map(vec3 x)
{
    const float a = 2.51;
    const float b = 0.03;
    const float c = 2.43;
    const float d = 0.59;
    const float e = 0.14;
    return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

void main()
{
    const uvec2 image_size = imageSize(accumulation_image);
    if (gl_GlobalInvocationID.x >= image_size.x || gl_GlobalInvocationID.y >= image_size.y)
    {
        return;
    }

    vec4 color = imageLoad(accumulation_image, ivec2(gl_GlobalInvocationID.xy));
    color.rgb = tone_map(color.rgb);
    color.a = clamp(color.a, 0.0, 1.0);
    imageStore(target_image, ivec2(gl_GlobalInvocationID.xy), color);
}
